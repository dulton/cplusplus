#
#  Copyright (c) 2007 by Spirent Communications Inc.
#  All Rights Reserved.
#
#  This software is confidential and proprietary to Spirent Communications Inc.
#  No part of this software may be reproduced, transmitted, disclosed or used
#  in violation of the Software License Agreement without the expressed
#  written consent of Spirent Communications Inc.
#
# $Id: //TestCenter/p2.30_dev_ifmgr/content/traffic/l2l3/il/Generator/Learning/SConscript#1 $
#

import os

Import('*')

sources = [ 'main.cpp' ]

class_sources = [
    'dpg_Autogen_port_server.cpp',
    'dpgIf_1_port_server.cpp',
    'dpg_1_port_server.cpp',
    'modifiers_1_port_server.cpp',
    'AddrInfo.cpp',
    'CodgramBlockAcceptor.cpp',
    'StreamBlockAcceptor.cpp',
    'DpgConnectionHandler.cpp',
    'DpgCodgramConnectionHandler.cpp',
    'DpgPodgramConnectionHandler.cpp',
    'DpgStreamConnectionHandler.cpp',
    'DpgDgramSocketHandler.cpp',
    'DpgApplication.cpp',
    'DpgApplicationProxy.cpp',
    'DpgMsgSetSrv_1.cpp',
    'DpgClientBlock.cpp',
    'DpgClientBlockLoadStrategies.cpp',
    'DpgServerBlock.cpp',
    'FlowConfigProxy.cpp',
    'PlaylistConfigProxy.cpp',
    'McoDriver.cpp',
    'RemAddrUtils.cpp',
    'SocketManager.cpp',
    'TcpPortUtils.cpp',
    'TimerManager.cpp',
    ]

mco_sources = [
    'statsdb.c'
    ]

xcginput = [ 
    ('l4l7Base_Autogen_port_server.h', '#content/traffic/l4l7/idl/l4l7Base_Autogen.idl'),
    ('l4l7Base_1_port_server.h', '#content/traffic/l4l7/idl/l4l7Base_1.idl'),
    ('dpg_Autogen_port_server.h', '#content/traffic/l4l7/idl/dpg_Autogen.idl'),
    ('dpg_Autogen_port_server.cpp', '#content/traffic/l4l7/idl/dpg_Autogen.idl'),
    ('modifiers_1_port_server.h', '#content/traffic/l4l7/idl/modifiers_1.idl'),
    ('modifiers_1_port_server.cpp', '#content/traffic/l4l7/idl/modifiers_1.idl'),
    ('dpgIf_1_port_server.h', '#content/traffic/l4l7/idl/dpgIf_1.idl'),
    ('dpgIf_1_port_server.cpp', '#content/traffic/l4l7/idl/dpgIf_1.idl'),
    ('dpg_1_port_server.h', '#content/traffic/l4l7/idl/dpg_1.idl'),
    ('dpg_1_port_server.cpp', '#content/traffic/l4l7/idl/dpg_1.idl'),
    ]

e = env.Clone()

# add dependencies on autogenerated header files
utils.xcgSetupAutogenDependencies(e, xcginput, class_sources)

for file, idl_path in xcginput :
    e.XCGBuilder(file, idl_path)

e.Depends('#content/traffic/l4l7/idl/dpg_Autogen.idl', '#content/traffic/l4l7/idl/l4l7Base_1.idl')
e.Depends('#content/traffic/l4l7/idl/dpg_1.idl', '#content/traffic/l4l7/idl/modifiers_1.idl')
e.Depends('#content/traffic/l4l7/idl/dpg_1.idl', '#content/traffic/l4l7/idl/dpg_Autogen.idl')
e.Depends('#content/traffic/l4l7/idl/dpg_1.idl', '#content/traffic/l4l7/idl/dpgIf_1.idl')

e.MCOBuilder(target=['statsdb.c', 'statsdb.h'], source='statsdb.mco')

e['CPPPATH'] += [ '.',
                  '#content/traffic/l4l7/il',
                  '#framework/il/common/lib', 
                  conf['eXtremeDB_srcdir'] + '/include',
                  ]

e['LIBS'].insert(0, 'bsdnet-preload')

e['LIBS'] += [ 'ACE',
               'frameworkidl',
               'hal++',
               'ifmgrclient',
               'ildaemon',
               'ilprotocol',
               'l4l7app',
               'l4l7base',
               'l4l7engine',
               'l4l7utils',
               'mps',
               'phxerrno',
               'phxexception',
               'phxlog',
               'phxrpc',
               'statsframework',
               'vif',
               ]
e['LIBS'] += e['LIB_DB']

binname = 'dpgd'

class_objs = []
for file in class_sources:
    class_objs += e.StaticObject(source=file)

mco_objs = []
for file in mco_sources :
    mco_objs += e.StaticObject(source=file)

bin = e.Program(os.path.join(conf['bin_dir'], binname), source=sources+class_objs+mco_objs)
utils.install(e, bin)

if env['utest'] :
    utest = SConscript(os.path.join('utest', 'SConscript'), [ 'class_objs', 'mco_objs' ])
    utils.coverageReport(e, utest)

# Install startup script
startup_script = 'dpg.sh'
script = e.Command(os.path.join(conf['script_startup_blddir'], 'stc', startup_script), startup_script, 'cp -f $SOURCE $TARGET')
utils.install(e,script)

# Local Variables:
# mode:python
# End:
# vim: syntax=python
